buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.novoda:sqlite-analyzer:0.3.2'
        classpath 'com.novoda:bintray-release:0.4.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.sqlite-analyzer'
apply plugin: 'bintray-release'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14

        def projects = rootProject.getProject().getChildProjects()
        projects.each {
            def project = it.value
            if (project.hasProperty('android')) {
                def authority = project.android.defaultConfig.manifestPlaceholders.downloadAuthority
                if (authority) {
                    buildConfigField "String", "DOWNLOAD_AUTHORITY", "\"${authority}\""
                }
            }
        }
    }

    testVariants.all {
        it.mergedFlavor.manifestPlaceholders = [downloadAuthority: "com.novoda.download-manager.android-test"]
    }

    lintOptions {
        abortOnError false
    }
}

sqliteAccess {
    migrationsDir 'src/main/assets/migrations'
    // todo fix this package name
    packageName 'com.novoda.downloadmanager.demo.simple'
}


dependencies {
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.novoda:notils:2.2.13'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.novoda:sqlite-provider:2.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'org.apache.commons:commons-io:1.3.2'
}

publish {
    userOrg = 'novoda'
    groupId = 'com.novoda'
    artifactId = 'download-manager'
    publishVersion = '100'
    description = 'Download manager based on AOSP DM but allowing downloading to internal private storage.'
    website = 'https://github.com/novoda/download-manager'
}
